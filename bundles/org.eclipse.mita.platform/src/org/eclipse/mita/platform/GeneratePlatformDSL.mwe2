module org.eclipse.mita.platform.GeneratePlatformDSL

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		scanClassPath = true
		registerGeneratedEPackage = "org.eclipse.mita.base.types.TypesPackage"
		registerGeneratedEPackage = "org.eclipse.mita.base.expressions.ExpressionsPackage"
	}
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.mita.platform"
				rootPath = rootPath
				eclipsePlugin = {
					enabled = true
				}
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "org.eclipse.mita.platform.PlatformDSL"
			fileExtensions = "platform"

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			referencedResource = "platform:/resource/org.eclipse.mita.platform/model/platforms.xcore"
			referencedResource = "platform:/resource/org.eclipse.mita.base/model/types.xcore"
			referencedResource = "platform:/resource/org.eclipse.mita.base/model/expressions.xcore"
		}
	}
}
