module org.eclipse.mita.program.GenerateProgramDsl

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.validation.*
import org.eclipse.mita.program.config.*

var rootPath = ".."

Workflow {
	bean = StandaloneSetup {
		scanClassPath = true
		registerGeneratedEPackage = "org.eclipse.mita.base.types.TypesPackage"
		registerGeneratedEPackage = "org.eclipse.mita.base.expressions.ExpressionsPackage"
	}
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.mita.program"
				rootPath = rootPath
				eclipsePlugin = {
					enabled = true
				}
				runtimeTest = {
					enabled = false
					overwriteSrc = false
				}
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "org.eclipse.mita.program.ProgramDsl"
			fileExtensions = "mita"
			referencedResource = "platform:/resource/org.eclipse.mita.platform/model/platforms.xcore"
			referencedResource = "platform:/resource/org.eclipse.mita.program/model/program.xcore"
			referencedResource = "platform:/resource/org.eclipse.mita.base/model/types.xcore"
			referencedResource = "platform:/resource/org.eclipse.mita.base/model/expressions.xcore"
			
			serializer = {
				generateStub = false
			}
			
			validator = {
				inheritImplementation = true
			}
			
			scopeProvider = {
				generateXtendStub = true
			}
			
			junitSupport = {
				useDeprecatedClasses = false
				generateStub = true
			}
		}
	}
}
