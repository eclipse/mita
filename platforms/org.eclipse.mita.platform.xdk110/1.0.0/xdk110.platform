
package platforms.xdk110;


enum BMA280_Range {
	/**< sets range to +/- 2G mode */
	Range_2G,
	
	/**< sets range to +/- 4G mode */
	Range_4G,
	
	/**< sets range to +/- 8G mode */
	Range_8G,
	
	/**< sets range to +/- 16G mode */
	Range_16G
}

enum BMA280_Bandwidth {
 	/**< sets bandwidth to LowPass 7.81HZ  */
	BW_7_81Hz,

	/**< sets bandwidth to LowPass 15.63HZ  */
	BW_15_63Hz,
	
	/**< sets bandwidth to LowPass 31.25HZ  */
	BW_31_25Hz,
	
	 /**< sets bandwidth to LowPass 62.50HZ  */
	BW_62_50Hz,
	
	/**< sets bandwidth to LowPass 125HZ  */
	BW_125Hz,
	
	/**< sets bandwidth to LowPass 250HZ  */
	BW_250Hz,
	
	/**< sets bandwidth to LowPass 500HZ  */
	BW_500Hz,
	
	/**< sets bandwidth to LowPass 1000HZ  */
	BW_1000Hz
}

/**
 * The BMA280 is a tri axial, low-g acceleration sensor with digital output for consumer applications.
 * It allows measurements of acceleration in three perpendicular axes.
 */
sensor BMA280 {
	generator "org.eclipse.mita.platform.xdk110.sensors.Bma280Generator"
	validator "org.eclipse.mita.platform.xdk110.sensors.Bma280Validator"
	
	/**
	 * The range of acceleration we want to measure.
	 */
	configuration-item range : BMA280_Range = BMA280_Range.Range_2G
	
	/**
	 * The low-pass filter bandwidth used by the BMA.
	 */
	configuration-item bandwidth : BMA280_Bandwidth = BMA280_Bandwidth.BW_500Hz
	
	/**
	 * The threshold of acceleration that has to be crossed before an any motion event is triggered.
	 */
	configuration-item any_motion_threshold : uint32 = 20
	
	/**
	 * The threshold of acceleration that must not be exceeded for a no motion event to be triggered.
	 */
	configuration-item no_motion_threshold : uint32 = 20
	
	
	/**
	 * The X axis of the BMA280
	 */
	modality x_axis : int32
	
	/**
	 * The Y axis of the BMA280
	 */
	modality y_axis : int32
	
	/**
	 * The Z axis of the BMA280
	 */ 
	modality z_axis : int32
	
	/**
	 * The L2 norm of the acceleration vector: sqrt(x^2 + y^2 + z^2)
	 */
	modality magnitude : uint32
	
	/**
	 * The any motion event (also called activity) uses the change between two successive
	 * acceleration measurements to detect changes in motion. An event is generated when this change
	 * exceeds the any_motion_threshold.
	 */
	event any_motion
	
	/**
	 * The no motion event (also called any inactivity) uses the change between two successive
	 * acceleration measurements to detect changes in motion. An event is generated when this change
	 * consecutively stays below the no_motion_threshold.
	 */
    event no_motion
    
    
    event low_g
    event high_g
    event single_tap
    event double_tap
    event flat
    event orientation
    event fifo_full
    event fifo_wml
    
    /**
     * This event serves the asynchronous reading of data. It is generated after storing a new value
     * of z-axis acceleration data in the data register.
     */
    event new_data
}

sensor GyroscopeSensorFusion {
	generator "org.eclipse.mita.platform.xdk110.sensors.GyroscopeSensorFusionGenerator"
	validator "org.eclipse.mita.platform.xdk110.sensors.GyroscopeSensorFusionValidator"
	
	/**
	 * The X axis of the gyroscope
	 */
	modality x_axis : int32
	
	/**
	 * The Y axis of the gyroscope
	 */
	modality y_axis : int32
	
	/**
	 * The Z axis of the gyroscope
	 */ 
	modality z_axis : int32
}

enum BMI160_Bandwidth
{              
	/**< sets bandwidth to LowPass 10.7 HZ */
    BW_10_7Hz, 
    /**< sets bandwidth to LowPass 20.8 HZ */
    BW_20_8Hz, 
    /**< sets bandwidth to LowPass 39.9 HZ */
    BW_39_9Hz, 
    /**< sets bandwidth to LowPass 74.6 HZ */
    BW_74_6Hz,  
    /**< sets bandwidth to LowPass 136.6 HZ */
    BW_136_6Hz, 
    /**< sets bandwidth to LowPass 254.6 HZ */
    BW_254_6Hz, 
    /**< sets bandwidth to LowPass 523.9 HZ */
    BW_523_9Hz, 
    /**< sets bandwidth to LowPass 890 HZ */
    BW_890Hz 
}

/**enum values to represent Range supported by the Gyroscope */
enum BMI160_BMG160_Range
{   /** set to 0.0625 deg/s in 125 deg/s range */
    Range_125s, 
    /** set to 0.125 deg/s in 250 deg/s range */
    Range_250s,  
    /** set to 0.25 deg/s in 500 deg/s range */
    Range_500s,  
    /** set to 0.5 deg/s in 1000 deg/s range */
    Range_1000s, 
    /** set to 1 deg/s in 2000 deg/s range */
    Range_2000s 
}

/**
 * The BMI160 is a small, low power, low noise 16-bit inertial measurement unit 
 * designed for use in mobile applications like augmented reality or indoor navigation 
 * which require highly accurate, real-time sensor data.
 */
sensor Gyroscope_BMI160 {
	generator "org.eclipse.mita.platform.xdk110.sensors.Bmi160Generator"
	validator "org.eclipse.mita.platform.xdk110.sensors.Bmi160Validator"
	
	/**
	 * The low-pass filter bandwidth used by the BMI160.
	 */
	configuration-item bandwidth : BMI160_Bandwidth = BMI160_Bandwidth.BW_39_9Hz

	/**
	 * The range the BMI160 should measure in.
	 */
	configuration-item range : BMI160_BMG160_Range = BMI160_BMG160_Range.Range_2000s
	
	/**
	 * The X axis of the BMI160
	 */
	modality x_axis : int32
	
	/**
	 * The Y axis of the BMI160
	 */
	modality y_axis : int32
	
	/**
	 * The Z axis of the BMI160
	 */ 
	modality z_axis : int32
	
}

enum BMG160_Bandwidth
{                                    
	/** sets bandwidth to LowPass 12 HZ */
    Bw_12Hz, 
    /** sets bandwidth to LowPass 23 HZ */
    Bw_23Hz, 
    /** sets bandwidth to LowPass 32 HZ */
    Bw_32Hz, 
    /** sets bandwidth to LowPass 47 HZ */
    Bw_47Hz, 
    /** sets bandwidth to LowPass 64 HZ */
    Bw_64Hz, 
    /** sets bandwidth to LowPass 116 HZ */
    Bw_116Hz,
    /** sets bandwidth to LowPass 230 HZ */
    Bw_230Hz,
    /** sets bandwidth to LowPass 523 HZ */
    Bw_523Hz 
}

/**
 * The BMG160 is an ultra-small, digital 3-axis angular rate sensor with a measurement range up to 2000Â°/s 
 * and a digital resolution of 16 bit for consumer electronics applications.
 */
sensor Gyroscope_BMG160 {
	generator "org.eclipse.mita.platform.xdk110.sensors.Bmg160Generator"
	validator "org.eclipse.mita.platform.xdk110.sensors.Bmg160Validator"
	
	/**
	 * The low-pass filter bandwidth used by the BMG160.
	 */
	configuration-item bandwidth : BMG160_Bandwidth = BMG160_Bandwidth.Bw_523Hz
	/**
	 * The range the BMG160 should measure in.
	 */
	configuration-item range : BMI160_BMG160_Range = BMI160_BMG160_Range.Range_2000s
	
	/**
	 * The X axis of the BMG160
	 */
	modality x_axis : int32
	
	/**
	 * The Y axis of the BMG160
	 */
	modality y_axis : int32
	
	/**
	 * The Z axis of the BMG160
	 */ 
	modality z_axis : int32
	
}


//
//sensor BMM150 {
//	generator ""
//}

sensor noise_sensor {
	generator "org.eclipse.mita.platform.xdk110.sensors.NoiseSensorGenerator"
	validator "org.eclipse.mita.platform.xdk110.sensors.NoiseSensorValidator"
	
	/**
	 * The sampling frequency in Hertz.
	 */
	configuration-item samplingFrequency: uint32 = 2560
	/**
	 * The timeout to wait for when getting noise in milliseconds.
	 */
	configuration-item timeout: uint32 = 100
	
	/**
	 * The noise the microphone of the XDK measures. This is unitless and needs to be calibrated in code yourself if you want absolute values. 
	 */
	modality noise: float
}

enum BME280_PowerMode {
	/**
	 * Normal mode comprises an automated perpetual cycling between an (active) measurement period and an (inactive) standby period.
	 */
	Normal,
	
	/**
	 * In forced mode, a single measurement is performed in accordance to the selected
	 * measurement and filter options. When the measurement is finished, the sensor returns to sleep
	 * mode and the measurement results can be obtained from the data registers.
	 */
	Forced
}

enum BME280_Oversampling {
	OVERSAMPLE_1X,
	OVERSAMPLE_2X,
	OVERSAMPLE_4X,
	OVERSAMPLE_8X,
	OVERSAMPLE_16X
}

/**
 * The BME280 is as combined digital humidity, pressure and temperature sensor based on proven sensing principles.
 */
sensor BME280 {
	generator "org.eclipse.mita.platform.xdk110.sensors.Bme280Generator"
	validator "org.eclipse.mita.platform.xdk110.sensors.Bme280Validator"
	
	/**
	 * The BME280 power mode
	 */
	configuration-item power_mode : BME280_PowerMode = BME280_PowerMode.Normal
	
	/**
	 * The standby time used in normal mode in milliseconds.
	 * Beware that the value supplied here will be clipped to the nearest valid value.
	 */
	configuration-item standby_time : uint32
	
	/**
	 * Reduces noise in the temperature measurement by over sampling.
	 * Higher over sampling settings reduce noise but increase measurement time and power consumption.
	 */
	configuration-item temperature_oversampling : BME280_Oversampling
	
	/**
	 * Reduces noise in the pressure measurement by over sampling.
	 * Higher over sampling settings reduce noise but increase measurement time and power consumption.
	 */
	configuration-item pressure_oversampling : BME280_Oversampling
	
	/**
	 * Reduces noise in the humidity measurement by over sampling.
	 * Higher over sampling settings reduce noise but increase measurement time and power consumption.
	 */
	configuration-item humidity_oversampling : BME280_Oversampling
	
	/**
	 * The temperature in milli-celcius
	 */
	modality temperature : int32
	
	/**
	 * The pressure in pascal
	 */
	modality pressure : uint32
	
	/**
	 * The relative humidity as percentage
	 */
	modality humidity : uint32
	
}

enum BMM150_Preset {                            
/**< ID to represent bmm150 Presetmode to lowpower  */        
	LowPower,    
	/**< ID to represent bmm150 Presetmode to regular  */         
    Regular,     
    /**< ID to represent bmm150 Presetmode to highaccuracy  */    
    HighAccuracy,
    /**< ID to represent bmm150 Presetmode to enhanced  */        
    Enhanced    
}
/**
 * The XDK's magnetometer
 */
sensor BMM150 {
	generator "org.eclipse.mita.platform.xdk110.sensors.Bmm150Generator"
	
	configuration-item mode: BMM150_Preset = BMM150_Preset.Regular
	
	modality x_axis: int32
    modality y_axis: int32
    modality z_axis: int32
    modality resistance: uint16
}

enum MAX44009_IntegrationTime {
	/**
	 * Integration time of 800 milliseconds. This is a preferred mode for boosting low-light sensitivity.
	 */
	MS_800,
	
	/**
	 * Integration time of 400 milliseconds. 
	 */
	MS_400,
	
	/**
	 * Integration time of 200 milliseconds. 
	 */
	MS_200,
	
	/**
	 * Integration time of 100 milliseconds. 
	 */
	MS_100,
	
	/**
	 * Integration time of 50 milliseconds. 
	 */
	MS_50,
	
	/**
	 * Integration time of 25 milliseconds. 
	 */
	MS_25,
	
	/**
	 * Integration time of 12.5 milliseconds. 
	 */
	MS_12_5,
	
	/**
	 * Integration time of 6.25 milliseconds. 
	 */
	MS_6_25
}

/**
 * The XDK light sensor.
 */
sensor MAX44009 {
	generator "org.eclipse.mita.platform.xdk110.sensors.Max44009Generator"
	validator "org.eclipse.mita.platform.xdk110.sensors.Max44009Validator"

	/**
	 * Enables the manual configuration of integration time and high brightness mode.
	 * If set to false (default) the chip automatically selects those values.
	 */
	configuration-item manual_mode : bool = false

	/**
	 * The integration time is the time the sensor collects light for. In automatic mode
	 * (manual mode set to false) the chip automatically selects the integration time.
	 *
	 * This configuration item is only used when manual_mode is set to true.
	 */
	configuration-item integration_time : MAX44009_IntegrationTime = MAX44009_IntegrationTime.MS_800
	
	/**
	 * Set to true so that only 1/8 of the photo diode current goes to the ADC. This mode is used in
	 * high-brightness situations to avoid saturation/clipping effects of the ADC.
	 *
	 * This configuration item is only used when manual_mode is set to true.
	 */
	configuration-item high_brightness : bool = false
	
	/**
	 * In default mode (false) the IC measures lux intensity only once every 800ms regardless
	 * of integration time. This mode allows the part to operate at its lowest possible supply current.
	 *
	 * In continuous mode (true) the IC continuously measures lux intensity. That is, as soon as one
	 * reading is finished, a new one begins. If integration time is 6.25ms, readings are taken every 6.25ms.
	 * If integration time is 800ms, readings are taken every 800ms. In this mode, the part consumes slightly
	 * higher power than in the default mode.
	 */
	configuration-item continuous_mode : bool = false
	
	/**
	 * The light intensity in millilux.
	 */
	modality intensity : uint32
}

//connectivity UART {
//	generator ""	
//}
//

/**
 * BLE is Bluetooth Low Energy using GATT profiles. 
 * At the moment we only support slave mode where the XDK becomes a device that can be connected to (as compared to connecting to some other device).
 */

export exception NoDeviceConnectedException;

connectivity named-singleton BLE {
	generator "org.eclipse.mita.platform.xdk110.connectivity.BleGenerator"
	validator "org.eclipse.mita.platform.xdk110.connectivity.BleValidator"
	
	/**
	 * The name of the device as advertised via GAP.
	 */
	configuration-item deviceName : string
	
	/**
	 * MAC Address to be configured. Format: "FC:D6:BD:xx:xx:xx" or "FC-D6-BD-xx-xx-xx".
	 */
	configuration-item macAddress : string

	/**
	 * The last four bytes of the UUID of the GATT service we'll create.
	 */
	configuration-item serviceUID : uint32
	
	/**
	 * The advertising interval in milliseconds.
	 */
	configuration-item advertisingInterval : int16 = 1000
	
	/**
	 * Creates a boolean GATT characteristic.
	 * 
	 * @param UUID The last four bytes of the characteristic UUID. Defaults to the hash code of the VCI name.
	 */
	signal bool_characteristic(UUID : uint32) : bool
	
	/**
	 * Creates an unsigned integer GATT characteristic.
	 * 
	 * @param UUID The last four bytes of the characteristic UUID. Defaults to the hash code of the VCI name.
	 */
	signal uint32_characteristic(UUID : uint32) : uint32
	
	/**
	 * Creates a signed integer GATT characteristic.
	 * 
	 * @param UUID The last four bytes of the characteristic UUID. Defaults to the hash code of the VCI name.
	 */
	signal int32_characteristic(UUID : uint32) : int32

}

/**
 * Senses actuation of a physical button.
 */
sensor Button {
	generator "org.eclipse.mita.platform.xdk110.sensors.ButtonGenerator"
	
	/**
	 * True if the button is pressed in this very right moment. False otherwise.
	 */
	modality is_pressed : bool
	
	/**
	 * Fires after the button was pressed.
	 */
	event pressed
	
	/**
	 * Fires after the button was released.
	 */
	event released
}

enum ADC_Channel {
	CH4,
    CH5,
    CH6,
    CH7,
    TEMP,
    VDDDIV3,
    VDD,
    VSS,
    VREFDIV2
}

enum ADC_Reference_Voltage {    
	/**< Internal 1.25V reference. */                     
	Ref_1V25,     
	/** Internal 2.5V reference. */                       
    Ref_2V5,         
    /** Buffered VDD. */                                  
    Ref_VDD,        
	/** Internal differential 5V reference. */            
    Ref_5VDIFF,    
    /** Single ended ext. ref. from pin 6. */             
    Ref_ExtSingle, 
    /** Differential ext. ref. from pin 6 and 7. */       
    Ref_ExtDiff, 
    /** Unbuffered 2xVDD. */                              
    Ref_2xVDD 
}

enum ADC_Resolution {  
	/**< 12 bit sampling. */
	Resolution_12Bit, 
	/**< 8 bit sampling. */  
    Resolution_8Bit,   
    /**< 6 bit sampling. */ 
    Resolution_6Bit,  
    /**< Oversampling. */    
    Resolution_OVS
}

enum ADC_SampleTime {  
	/**< 1 clock cycle. */  
	Time_1,            
	/**< 2 clock cycle. */  
    Time_2,            
    /**< 4 clock cycle. */  
    Time_4,            
    /**< 8 clock cycle. */  
    Time_8,            
    /**< 16 clock cycle. */ 
    Time_16,           
    /**< 32 clock cycle. */ 
    Time_32,           
    /**< 64 clock cycle. */ 
    Time_64,           
    /**< 128 clock cycle. */
    Time_128,          
    /**< 256 Clock cycle. */
    Time_256
}

/**
 * The XDK's ADC (Analog Digital Converter)
 */
connectivity named-singleton ADC {
	generator "org.eclipse.mita.platform.xdk110.connectivity.AdcGenerator"
	validator "org.eclipse.mita.platform.xdk110.connectivity.AdcValidator"
	
	/**
	 * The reference voltage to measure against with referenceVoltage one of Ref_ExtSingle or Ref_ExtDiff in [mV].
	 */
	configuration-item externalReferenceVoltage: uint16
	
	/**
	 * an ADC channel. Read only. [mV]
	 */
	signal channel(channel: ADC_Channel, 
		referenceVoltage: ADC_Reference_Voltage = ADC_Reference_Voltage.Ref_2V5, 
		resolution: ADC_Resolution = ADC_Resolution.Resolution_12Bit, 
		sampleTime: ADC_SampleTime = ADC_SampleTime.Time_16
	): uint16
}

alt WlanAuthentication {
	None 
	/**
     * WPA(2) with PSK (pre-shared key) 
     */
	| Personal : {
		psk : string
	}  
	/**
	 * WPA(2) Enterprise with username/password
	 */
	| Enterprise : {
		username : string,
		password : string
	}
}

alt IpConfiguration {
	Dhcp | Static : {
		/**
	     * The static IP address to use. This field has to be in the number-dot
	     * customary for IPv4 addresses (see IETF draft-main-ipaddr-text-rep-02).
	     */
		ip : string,   
	    /**
	     * The IP address of the default Subnet Mask. This field has to be in the number-dot
	     * customary for IPv4 addresses (see IETF draft-main-ipaddr-text-rep-02).
	     */
		subnetMask : string,
		/**
	     * The IP address of the default gateway. This field has to be in the number-dot
	     * customary for IPv4 addresses (see IETF draft-main-ipaddr-text-rep-02).
		 */
		gateway : string,
		/**
	     * The IP address of the default DNS server. This field has to be in the number-dot
	     * customary for IPv4 addresses (see IETF draft-main-ipaddr-text-rep-02).
	     */
		dns : string
	}
}

enum LoRaMessageConfirmation {
	Confirmed, Unconfirmed
}

alt CayennePayload {
	  DigitalInput: uint8
	| DigitalOutput: uint8
	| AnalogInput: int16
	| AnalogOutput: int16
	| IlluminanceSensor: uint16
	| PresenceSensor: uint8
	| TemperatureSensor: int16
	| HumiditySensor: uint8
	| Accelerometer: int16, int16, int16
	| Barometer: uint16
	| Gyrometer: int16, int16, int16
	| GpsLocation: {Latitude: int32, Longitude: int32, Altitude: int32}
}

enum Region {
	EU, US
}

connectivity named-singleton LoRa {
	generator "org.eclipse.mita.platform.xdk110.connectivity.LoraGenerator"
    validator "org.eclipse.mita.platform.xdk110.connectivity.LoraValidator"

	required configuration-item region: Region
	required configuration-item loraAppKey: array<uint8>
	required configuration-item loraAppEui: array<uint8>
	
	configuration-item loraDeviceEui: array<uint8>
	configuration-item adaptiveDataRate: bool = true
	
	configuration-item bandFrequency: uint16
	configuration-item rx2Frequency: uint32
	configuration-item rx2DataRate: uint8
	configuration-item dataRate: uint8 = 3 
	
	
	
	signal raw(portNum: uint8 = 1, confirmation: LoRaMessageConfirmation = LoRaMessageConfirmation.Unconfirmed): array<uint8>
	signal cayenne(portNum: uint8 = 1, confirmation: LoRaMessageConfirmation = LoRaMessageConfirmation.Unconfirmed): array<CayennePayload>
}

/**
 * Specifies the connection to a wireless wide-area network, also refered to as WiFi. 
 */
connectivity named-singleton WLAN {
    generator "org.eclipse.mita.platform.xdk110.connectivity.WlanGenerator"
    validator "org.eclipse.mita.platform.xdk110.connectivity.WlanValidator"

    /**
     * Choose personal WPA connection or Enterprise WPA connection
     */    
    required configuration-item authentication : WlanAuthentication

    /**
     * The SSID of the WLAN network we want to connect to
     */
    required configuration-item ssid : string
       
    /**
     * If true server certificate will be uploaded to the WiFi chip CC3100. 
     * Make sure to update service pack of the WiFi and then upload the certificate.
     * Certificate must placed under XDK110/common/certs/XDKDummy.
     */
    configuration-item isHostPgmEnabled : bool = false
    
    /**
     * Configure IP address and network via DHCP or static 
     */
    configuration-item ipConfiguration : IpConfiguration = Dhcp()
}

exception MqttException;

alt MqttAuthentication {
	None | Login: {username: string, password: string}
}

alt MqttWill {
	NoWill | LastWill: {topic: string, message: string, qos: uint8} 
}

/**
 * MQTT (Message Queuing Telemetry Transport) is an ISO standard (ISO/IEC PRF 20922) publish-subscribe-based messaging protocol.
 * This MQTT connectivity requires WLAN to be setup as well. 
 */
connectivity many MQTT {
	generator "org.eclipse.mita.platform.xdk110.connectivity.MqttGenerator"
    validator "org.eclipse.mita.platform.xdk110.connectivity.MqttValidator"
	
	/**
	 * The underlying transport connectivity we use to send the data out. In the future we might also
	 * support LoRa here.
	 */
	required configuration-item transport : WLAN
	
	/**
	 * The URL pointing to the MQTT broker, for example: mqtt://does-not.exist:8000
	 */
	required configuration-item url : string
	
	/**
	 * A time server to use to get the current time. This is needed to allow certificate validation for MQTTS.
	 */
	configuration-item sntpServer: string = "sntp://pool.ntp.org:123"
	
	/**
	 * The path to the server's public certificate to use for authentication. PEM format. Absolute or relative to the project root.
	 */
	configuration-item certificatePath: string
	
	/**
	 * The client identifier (here: clientID) is a identifier of each MQTT client
     * connecting to a MQTT broker. It needs to be unique for the broker to
     * know the state of the client. 
	 */
	required configuration-item clientId : string
	
	configuration-item lastWill: MqttWill = NoWill()
	
	configuration-item authentication: MqttAuthentication = None()
	
	/**
	 * The clean session flag indicates to the broker whether the client wants
     * to establish a clean session or a persistent session where all
     * subscriptions and messages (QoS 1 & 2) are stored for the client.
	 */
	configuration-item cleanSession : bool = false
	
	/**
	 * The keep alive interval (in seconds) is the time the client commits to for
     * when sending regular pings to the broker. The broker responds to the
     * pings enabling both sides to determine if the other one is still alive and
     * reachable.
     */
	configuration-item keepAliveInterval : uint32 = 60
	
	/**
	 * Publishes a message to a particular topic. 
	 */
	signal topic(name : string, qos : uint32 = 0) : string
	
}

alt HonoAuthentication {
	Authenticated: { username: string, password: string }
	| Unauthenticated: { tenant: string, deviceId: string }
}

connectivity many HonoMqtt {
	generator "org.eclipse.mita.platform.xdk110.connectivity.HonoGenerator"
    validator "org.eclipse.mita.platform.xdk110.connectivity.MqttValidator"
	
	/**
	 * The underlying transport connectivity we use to send the data out. In the future we might also
	 * support LoRa here.
	 */
	required configuration-item transport : WLAN
	
	/**
	 * The URL pointing to the MQTT broker, for example: mqtt://does-not.exist:8000
	 */
	required configuration-item url : string
	
	/**
	 * A time server to use to get the current time. This is needed to allow certificate validation for MQTTS.
	 */
	configuration-item sntpServer: string = "sntp://pool.ntp.org:123"
	
	/**
	 * The client identifier (here: clientID) is a identifier of each MQTT client
     * connecting to a MQTT broker. It needs to be unique for the broker to
     * know the state of the client. 
	 */
	required configuration-item clientId : string
	
	/**
	 * The path to the server's public certificate to use for authentication. PEM format. Absolute or relative to the project root.
	 */
	configuration-item certificatePath: string
	
	required configuration-item authentication: HonoAuthentication
	
	/**
	 * Publishes a Hono telemetry message 
	 */
	signal telemetry(qos: uint32 = 0) : string
	
	/**
	 * Publishes a Hono event. 
	 */
	signal ^event() : string
}

/**
 * Exception thrown when we can't setup the WLAN network
 */
exception HttpRequestNotOkException;

/**
 * The HttpMethods supported by the XDK
 */
enum HttpMethod {
	GET, POST, PUT, DELETE, UPDATE
}

/**
 * Implements an HTTP REST Client over WIFI 
 */
connectivity many HttpRestClient {
	generator "org.eclipse.mita.platform.xdk110.connectivity.RestClientGenerator"
	validator "org.eclipse.mita.platform.xdk110.connectivity.RestClientValidator"
	
	/**
	 * The underlying transport connectivity we use to send the data out. In the future we might also
	 * support LoRa here.
	 */
	required configuration-item transport : WLAN
	
	/**
	 * The part of the endpoint URL common to all requests, e.g. http://foobar.com/api/v1
	 */
	required configuration-item endpointBase : string
	
	/**
	 * A custom header which is added to each HTTP request. Example:
	 *   X-Auth: MySecretToken\nX-Version: 1.0 
	 */
	configuration-item headerContent : string

	signal resource(endpoint : string, contentType : string = "application/json", writeMethod : HttpMethod = HttpMethod.POST, readMethod : HttpMethod = HttpMethod.GET) : string
	
}


enum LedColor {
	Red,
	Yellow,
	Orange
}

/**
 * Displays state using three differently colored LEDs on the device.
 */
connectivity named-singleton LED {
	generator "org.eclipse.mita.platform.xdk110.connectivity.LedGenerator"
	validator "org.eclipse.mita.platform.xdk110.connectivity.LedValidator"
	
	signal light_up(color : LedColor) : bool
}

enum ByteOrder {
	LittleEndian,
	BigEndian
}

enum I2CMode {
	None, 
	Read, 
	Write,
	ReadWrite
}

bus many I2C {
	generator "org.eclipse.mita.platform.xdk110.buses.I2CGenerator"
	validator "org.eclipse.mita.platform.xdk110.buses.I2CValidator"
	sizeInferrer "org.eclipse.mita.program.inferrer.GenericPlatformSizeInferrer"
	
	required configuration-item deviceAddress: uint8
	
	configuration-item byteOrder : ByteOrder = ByteOrder.LittleEndian
	
	signal array_register_uint8(address: uint8, length: uint8, mode: I2CMode = I2CMode.ReadWrite): array<uint8>
	signal array_register_int8(address: uint8, length: uint8, mode: I2CMode = I2CMode.ReadWrite): array<int8>
	signal array_register_uint16(address: uint8, length: uint8, mode: I2CMode = I2CMode.ReadWrite): array<uint16>
	signal array_register_int16(address: uint8, length: uint8, mode: I2CMode = I2CMode.ReadWrite): array<int16>
	signal array_register_uint32(address: uint8, length: uint8, mode: I2CMode = I2CMode.ReadWrite): array<uint32>
	signal array_register_int32(address: uint8, length: uint8, mode: I2CMode = I2CMode.ReadWrite): array<int32>
	
	signal register_uint8(address: uint8, mode: I2CMode = I2CMode.ReadWrite): uint8
	signal register_int8(address: uint8, mode: I2CMode = I2CMode.ReadWrite): int8
	signal register_uint16(address: uint8, mode: I2CMode = I2CMode.ReadWrite): uint16
	signal register_int16(address: uint8, mode: I2CMode = I2CMode.ReadWrite): int16
	signal register_uint32(address: uint8, mode: I2CMode = I2CMode.ReadWrite): uint32
	signal register_int32(address: uint8, mode: I2CMode = I2CMode.ReadWrite): int32
}

enum GPIOPin
{   /** Alternate function shall be Timer0 channel 0 compare operations */
    PA0,  
    /** General purpose I/O  */
    PA1,  
    /** General purpose I/O  */
    PB2,  
    /** General purpose I/O, SPI MOSI operations  */
    PB3,  
    /** General purpose I/O, SPI MISO operations  */
    PB4,  
    /** General purpose I/O, SPI clock operations */
    PB5,  
    /** General purpose I/O, UART1_TX operations */
    PB9,  
    /** General purpose I/O, UART1_RX operations */
    PB10, 
    /** General purpose I/O, Timer0 channel 1 compare operations  */
    PC0,  
    /** General purpose I/O, Timer0 channel 2 compare operations  */
    PC1,  
    /** General purpose I/O, Timer0 Channel 0 Complementary Dead-Time Insertion operations  */
    PC2,  
    /** General purpose I/O, Timer0 Channel 1 Complementary Dead-Time Insertion operations   */
    PC3,  
    /** General purpose I/O, Timer0 Channel 2 Complementary Dead-Time Insertion operations  */
    PC4,  
    /** General purpose I/O, Timer2 channel 0 capture operations   */
    PC8,  
    /** General purpose I/O, Timer2 channel 1 capture operations   */
    PC9,  
    /** General purpose I/O, Timer2 channel 2 capture operations   */
    PC10, 
    /** General purpose I/O, ADC0 operations */
    PD5,  
    /** General purpose I/O, ADC0 operations */
    PD6,  
    /** General purpose I/O */
    PD8,  
    /** General purpose I/O */
    PE2,  
    /** General purpose I/O */
    PF6
}

enum GPIOMode {                    
	/** Input enabled */
	NoPull,               
	/** Input enabled with internal pull-up */
    PullUp,               
    /** Input enabled with internal pull-down */
    PullDown,             
    /** Input enabled with internal pull-up and glitch filter enabled  */
    PullUpFilter
}

bus named-singleton GPIO {
	generator "org.eclipse.mita.platform.xdk110.buses.GPIOGenerator"
	
	signal digitalIn(pin : GPIOPin, mode : GPIOMode) : bool
	signal digitalOut(pin : GPIOPin, initialValue : bool = false) : bool
}

export exception EndOfFileException;

io named-singleton SDCard {
	generator "org.eclipse.mita.platform.xdk110.io.SDCardGenerator"
	sizeInferrer "org.eclipse.mita.platform.xdk110.io.SDCardSizeInferrer"

	signal resumingBinaryRead(filePath: string, blockSize: uint32) : array<uint8>
	signal appendingBinaryWrite(filePath: string) : array<uint8>

	signal resumingTextRead(filePath: string, blockSize: uint32) : string
	signal appendingTextWrite(filePath: string) : string

	signal rewindingBinaryRead(filePath: string, fileSize: uint32) : array<uint8>
	signal rewindingBinaryWrite(filePath: string) : array<uint8>

	signal rewindingTextRead(filePath: string, fileSize: uint32) : string
	signal rewindingTextWrite(filePath: string) : string
}


alias environment for BME280
alias accelerometer for BMA280
alias gyroscope for GyroscopeSensorFusion
alias light for MAX44009
alias button_one for Button
alias button_two for Button
alias magnetometer for BMM150

alt PowerStatus {
	  Battery: float
	| Corded
}

platform XDK110 {
	module "org.eclipse.mita.platform.xdk110.platform.Xdk110PlatformGeneratorModule"
	generator "org.eclipse.mita.platform.xdk110.platform.Xdk110PlatformGenerator"
	validator "org.eclipse.mita.platform.xdk110.platform.Validation"

	has environment
	has accelerometer
	has gyroscope
	has magnetometer
	has light
	has button_one
	has button_two
	has noise_sensor
	
	has Gyroscope_BMI160
	has Gyroscope_BMG160
	
	has I2C
	has GPIO
	has ADC
	has BLE
	has LED
	has WLAN
	has MQTT
	has HonoMqtt
	has HttpRestClient
	has LoRa
	
	has SDCard

	configuration-item applicationName : string = "EclipseMitaApplication"
	/**
	 * Enabling this powers the 2.5V and 3.3V external power on the extension bus. 
	 */
	configuration-item powerExternalDevices: bool = false
	/**
	 * Configure the XDK to wait some time in milliseconds before setting up and enabling resources.
	 * This is especially useful to view debug output.
	 */
	configuration-item startupDelay: uint32 = 0
	
	/**
	 * The FreeRTOS stack size of the event loop task in 32-bit words. If you don't know what this
	 * means, be careful when you change this value. More information can be found here:
	 * http://www.freertos.org/FAQMem.html#StackSize
	 */
	configuration-item stackSize: uint32 = 2000
	
	modality powerStatus: PowerStatus
	
	event startup
}